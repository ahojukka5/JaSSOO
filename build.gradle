plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.0.0'
    id 'pmd'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

configurations {
    checkstyleConfig
}

def versions = [
    checkstyle: '8.10.1',
]

repositories {
    jcenter() 
}

dependencies {
    testImplementation 'junit:junit:4.12' 
    checkstyleConfig ("com.puppycrawl.tools:checkstyle:${versions.checkstyle}") {
        transitive = false
    }
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
}

checkstyle {
    showViolations = true
    ignoreFailures = false
    toolVersion = "${versions.checkstyle}"
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

mainClassName = 'Main'
